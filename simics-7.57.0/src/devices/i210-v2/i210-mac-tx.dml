/*
  i210-mac-tx.dml

  Â© 2013 Intel Corporation

  This software and the related documents are Intel copyrighted materials, and
  your use of them is governed by the express license under which they were
  provided to you ("License"). Unless the License provides otherwise, you may
  not use, modify, copy, publish, distribute, disclose or transmit this software
  or the related documents without Intel's prior written permission.

  This software and the related documents are provided as is, with no express or
  implied warranties, other than those that are expressly stated in the License.
*/

dml 1.4;

attribute context is (advanced_context_attr);

bank csr {
    group tx_queue [i < NUM_TRANSMIT_QUEUES] {
        is tx_desc_queue;
        param BA = 0xe000 + i * 0x40;
    }

    group tx_queue_alias [i < NUM_TRANSMIT_QUEUES] {
        is tx_desc_queue_alias;
        param BA = 0x03800 + i * 0x100;
        param reggroup = tx_queue;
    }

    register tctl_ext @ 0x00404 "Transmit Control Extended";
    register txpbs @ 0x03404 "TX packet buffer size";
    register dtxswc @ 0x03500 "DMA TX switch control";
    register dtxctl @ 0x03590 "DMA TX control";

    register tctl {
        field en @ [1] "Transmit Enable" {
            is write;
            method write(uint64 value) {
                if (value == 0 && this.val == 1) {
                    this.val = 0;
                    log info, 2: "disable MAC transceiver";
                } else if (this.val == 0 && value == 1) {
                    this.val = 1;
                    log info, 2: "enable MAC transceiver";
                    transmit();
                }
            }
        }
        field psp @ [3] "Pad Short Packets" {
            param init_val = 1;  // NOTE: real reset value is 0
        }
        field ct @ [11:4] "Collision Threshold" {
            param init_val = 0xf; // NOTE: real reset value is 0
        }
        field bst @ [21:12] "Back-Off Slot Time" {
            param init_val = 0x40; // NOTE: real reset value is 0
        }
        field swxoff @ [22] is (unimpl) "Software XOFF Transmission";
        // reserved[23]
        field rtlc @ [24]   is (unimpl) "Re-transmit on Late Collision";
        // reserved[31:25]
    }

    register tctl_ext {
        field cold @ [19:10] "Collision Distance" {
            param init_val = 0x42;
        }
    }

    register txpbs {
        field pbsize @ [5:0] is (unimpl) "Packet Buffer Size" {
            param init_val = 0x28;
        }
    }

    register dtxswc is (unimpl);
    register dtxctl is (unimpl);

}
